## snakemake for analysis the dna sequences of the zinc fingers protein detected with process_zincfinger.smk
## Before run process_zincfinger_dna.smk
## First run process_stats_domain.smk
## Then run process_zincfinger.smk
##
## Authors :


# Import python modules
# ---------------------
import os
import json

# Function to load JSON files
# ---------------------------
def load_json(file_path):
    with open(file_path, "r") as file:
        return json.load(file)

# Assign environment variables
# ----------------------------
globals().update(load_json("../environment_path.json"))


# Configuration
# -------------
configfile: "analyse_zf.json"
configfile: "assemblies.json"

# List of assemblies
# ------------------
ACCESSNB = config["assembly_list"]




RESOURCES_DIR_NAME  = config["resources_dir_name"]

# The reference alignment of each domain
# ---------------------------------------
DOMAIN_REFERENCES = config["domain_references"]

# List of domains to be fully processed.
# --------------------------------------
DOMAINS = config["domains"]

# List of domains to be processed without paralogy checks.
# --------------------------------------------------------
DOMAINS_SIMPLE = config["domains_simple"]

# Name of global results directory.
# The directory is located in pathGTDriftGlobalResults
# ----------------------------------------------------
GLOBAL_RESULTS = config["analyse_dir_name"]

# Name of genome specific results directory.
# The directory is located in genome_assembly/{accession}/analyses/
# -----------------------------------------------------------------
GENOME_RESULTS = config["analyse_dir_name"]

storagetype  = config["storagetype"]




# function to get the name of the reference alignment for a domain
# ----------------------------------------------------------------
def get_domain(wildcards):
    domain = wildcards.domain
    return domain

# function to get the name of the reference alignment for a domain
# ----------------------------------------------------------------
def get_reference(wildcards):
    fname = DOMAIN_REFERENCES.get(wildcards.domain, "")
    return fname

# function to get the path of the reference alignment for a domain
# ----------------------------------------------------------------
def get_reference_file(wildcards):
    fname = DOMAIN_REFERENCES.get(wildcards.domain, "")
    domain = wildcards.domain
    return pathGTDriftResource + RESOURCES_DIR_NAME + "hmm_profiles/"+ domain+"/"+fname+".hmm"

# get the files and directories describing the reference alignments
# -----------------------------------------------------------------
directories, files = glob_wildcards(pathGTDriftResource + RESOURCES_DIR_NAME + "reference_alignments/{dir}/{file}.fst")

# Rules
# -----

# -----------------------------------------------
# all : inputs define the to be files generated .
# -----------------------------------------------
rule all:
    input:
        zincfinger_motif = expand(pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/zf_results.csv",accession=ACCESSNB),
        fasta_list = expand(pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/fasta/list_of_files.txt",accession=ACCESSNB),
        zincfinger_out = expand(pathGTDriftGlobalResults + GLOBAL_RESULTS + "zinc_finger_dna/zinc_finger_details/ZFD_{accession}_global.csv",accession=ACCESSNB),

# --------------------------------------
# get_genome_seq_fasta
# get the fasta sequence of the assembly
# --------------------------------------
rule get_genome_seq_fasta:
    input:
        # path to the fasta file
        fasta = pathGTDriftData+ "genome_assembly/{accession}/genome_seq/genomic.fna.path"
    output:
        # fasta file
        fasta = temp(pathGTDriftData+ "genome_assembly/{accession}/genome_seq/genomic.fna")
    shell:
        """
        export  genomic=`cat {input.fasta}`
        echo "Genome sequence fasta file : $genomic"
        if [ {storagetype} == irods ];
            then
            echo "iget  /lbbeZone/home/penel/gtdrift/genome_seq/$genomic"
            ls {pathGTDriftData}"genome_assembly/{wildcards.accession}/genome_seq/"
            iget -f /lbbeZone/home/penel/gtdrift/genome_seq/$genomic {output.fasta}
        else
            echo "symbolic link  {pathGTDriftData}genome_assembly/{wildcards.accession}/genome_seq/$genomic to  {output.fasta}"
            ln -s {pathGTDriftData}genome_assembly/{wildcards.accession}/genome_seq/$genomic {output.fasta}
        fi
        """

# -------------------------------------------------------
# zincfinger_motif
# analyse of the dna sequences of the zincfinger proteins
# -------------------------------------------------------
rule zincfinger_motif:
    """
    Run the zinc finger dna analysis on each protein sequence.
    """
    input:
        # Candidates generated by process_zincfinger.smk
        # ----------------------------------------------
        protein_seq = pathGTDriftData
            + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS
            + "candidates_for_zf_analysis.fasta",
        # gff file of the assembly
        gff = pathGTDriftData + "genome_assembly/{accession}/annotation/genomic.gff",
        # fasta of the dna sequences of the assembly
        fasta_dna = pathGTDriftData+ "genome_assembly/{accession}/genome_seq/genomic.fna",
    output:
        results = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/zf_results.csv",
        log = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/zf_results.log",
        warnings = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/zf_results.warnings",
    shell:
        """
        python3 ../utils/python/get_ZF_motif_positions_in_protein.py -i {input.protein_seq} -g {input.gff}  -d {input.fasta_dna} -o {output.results} -l {output.log} -w {output.warnings}
        """


# -------------------------------------------------------
# zincfinger_motif
# analyse of the dna sequences of the zincfinger proteins
# -------------------------------------------------------
rule get_fasta:
    """
    get the fasta file of zinc fingers.
    """
    input:
        results = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/zf_results.csv",
    output:
        fasta_list = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/fasta/list_of_files.txt",
        #max_cluster_fasta = pathGTDriftData + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS +  "zinc_finger_dna/fasta/max_cluster.fasta",
    shell:
        """
        python3 ../utils/python/extract_zf_fasta.py -i {input.results} -o {pathGTDriftData}genome_assembly/{wildcards.accession}/analyses/{GENOME_RESULTS}zinc_finger_dna/fasta
        """
        
        
        
        
rule zincfinger_analysis_global:
    """
    Run the zinc finger analysis on each protein sequence using R.
    """
    input:
        # Candidates after paralog checks
        # --------------------------------
        protein_seq = pathGTDriftData
            + "genome_assembly/{accession}/analyses/" + GENOME_RESULTS
            + "candidates_for_zf_analysis.fasta",
        #protein_seq = pathGTDriftData + "genome_assembly/{accession}/analyses/prdm9_prot/candidates_prdm9.fasta"
    output:
        zincfinger_out = pathGTDriftGlobalResults + GLOBAL_RESULTS + "zinc_finger_dna/zinc_finger_details/ZFD_{accession}_global.csv"

    run:
        # Verify if the input file exists
        if not os.path.exists(input.protein_seq):
            raise FileNotFoundError(f"Input file does not exist: {input.protein_seq}")

        # Run the R script
        command = f"Rscript --vanilla ../utils/zincfinger_analysis.R {input.protein_seq} {output.zincfinger_out}"
        shell(command)
